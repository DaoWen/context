/*
            Copyright Oliver Kowalke 2009.
   Distributed under the Boost Software License, Version 1.0.
      (See accompanying file LICENSE_1_0.txt or copy at
          http://www.boost.org/LICENSE_1_0.txt)
*/

/******************************************************
 *                                                     *
 *  -------------------------------------------------  *
 *  |  0  |  1  |  2  |  3  |  4  |  5  |  6  |  7  |  *
 *  -------------------------------------------------  *
 *  |  0  |  4  |  8  |  12 |  16 |  20 |  24 |  28 |  *
 *  -------------------------------------------------  *
 *  |    F14    |    F15    |    F16    |    F17    |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  8  |  9  |  10 |  11 |  12 |  13 |  14 |  15 |  *
 *  -------------------------------------------------  *
 *  |  32 |  36 |  40 |  44 |  48 |  52 |  56 |  60 |  *
 *  -------------------------------------------------  *
 *  |    F18    |    F19    |    F20    |    F21    |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  16 |  17 |  18 |  19 |  20 |  21 |  22 |  23 |  *
 *  -------------------------------------------------  *
 *  |  64 |  68 |  72 |  76 |  80 |  84 |  88 |  92 |  *
 *  -------------------------------------------------  *
 *  |    F22    |    F23    |    F24    |    F25    |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  24 |  25 |  26 |  27 |  28 |  29 |  30 |  31 |  *
 *  -------------------------------------------------  *
 *  |  96 | 100 | 104 | 108 | 112 | 116 | 120 | 124 |  *
 *  -------------------------------------------------  *
 *  |    F26    |    F27    |    F28    |    F29    |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  32 |  33 |  34 |  35 |  36 |  37 |  38 |  39 |  *
 *  -------------------------------------------------  *
 *  | 128 | 132 | 136 | 140 | 144 | 148 | 152 | 156 |  *
 *  -------------------------------------------------  *
 *  |    F30    |    F31    |   fpscr   | R13 | R14 |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  40 |  41 |  42 |  43 |  44 |  45 |  46 |  47 |  *
 *  -------------------------------------------------  *
 *  | 160 | 164 | 168 | 172 | 176 | 180 | 184 | 188 |  *
 *  -------------------------------------------------  *
 *  | R15 | R16 | R17 | R18 | R19 | R20 | R21 | R22 |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  48 |  49 |  50 |  51 |  52 |  53 |  54 |  55 |  *
 *  -------------------------------------------------  *
 *  | 192 | 196 | 200 | 204 | 208 | 212 | 216 | 220 |  *
 *  -------------------------------------------------  *
 *  | R23 | R24 | R25 | R26 | R27 | R28 | R29 | R30 |  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  56 |  57 |  58 |  59 |  60 |  61 |  62 |  63 |  *
 *  -------------------------------------------------  *
 *  | 224 | 228 | 232 | 236 | 240 | 244 | 248 | 252 |  *
 *  -------------------------------------------------  *
 *  | R31 |hiddn|  CR |  LR |  PC |bchai|linkr| FCTX|  *
 *  -------------------------------------------------  *
 *  -------------------------------------------------  *
 *  |  64 |                                         |  *
 *  -------------------------------------------------  *
 *  | 256 |                                         |  *
 *  -------------------------------------------------  *
 *  | DATA|                                         |  * 
 *  -------------------------------------------------  *
 *                                                     *
 *******************************************************/
.globl .jump_fcontext
.globl  jump_fcontext[DS]
.align 2 
.csect	jump_fcontext[DS]
jump_fcontext:
  .long	.jump_fcontext
.jump_fcontext:
    # reserve space on stack
    subi  1, 1, 244

    stfd  14, 0(1)  # save F14
    stfd  15, 8(1)  # save F15
    stfd  16, 16(1)  # save F16
    stfd  17, 24(1)  # save F17
    stfd  18, 32(1)  # save F18
    stfd  19, 40(1)  # save F19
    stfd  20, 48(1)  # save F20
    stfd  21, 56(1)  # save F21
    stfd  22, 64(1)  # save F22
    stfd  23, 72(1)  # save F23
    stfd  24, 80(1)  # save F24
    stfd  25, 88(1)  # save F25
    stfd  26, 96(1)  # save F26
    stfd  27, 104(1)  # save F27
    stfd  28, 112(1)  # save F28
    stfd  29, 120(1)  # save F29
    stfd  30, 128(1)  # save F30
    stfd  31, 136(1)  # save F31
    mffs  0  # load FPSCR
    stfd  0, 144(1)  # save FPSCR

    stw  13, 152(1)  # save R13
    stw  14, 156(1)  # save R14
    stw  15, 160(1)  # save R15
    stw  16, 164(1)  # save R16
    stw  17, 168(1)  # save R17
    stw  18, 172(1)  # save R18
    stw  19, 176(1)  # save R19
    stw  20, 180(1)  # save R20
    stw  21, 184(1)  # save R21
    stw  22, 188(1)  # save R22
    stw  23, 192(1)  # save R23
    stw  24, 196(1)  # save R24
    stw  25, 200(1)  # save R25
    stw  26, 204(1)  # save R26
    stw  27, 208(1)  # save R27
    stw  28, 212(1)  # save R28
    stw  29, 216(1)  # save R29
    stw  30, 220(1)  # save R30
    stw  31, 224(1)  # save R31
    stw  3,  228(1)  # save hidden

    # save CR
    mfcr  0
    stw   0, 232(1)
    # save LR
    mflr  0
    stw   0, 236(1)
    # save LR as PC
    stw   0, 240(1)

    # store RSP (pointing to context-data) in R6
    mr  6, 1

    # restore RSP (pointing to fcontext_t) from R4
    lwz  1, 0(4)
    # set fcontext_t to nullptr
    li   0, 0
    stw  0, 0(4)

    lfd  14, 0(1)  # restore F14
    lfd  15, 8(1)  # restore F15
    lfd  16, 16(1)  # restore F16
    lfd  17, 24(1)  # restore F17
    lfd  18, 32(1)  # restore F18
    lfd  19, 40(1)  # restore F19
    lfd  20, 48(1)  # restore F20
    lfd  21, 56(1)  # restore F21
    lfd  22, 64(1)  # restore F22
    lfd  23, 72(1)  # restore F23
    lfd  24, 80(1)  # restore F24
    lfd  25, 88(1)  # restore F25
    lfd  26, 96(1)  # restore F26
    lfd  27, 104(1)  # restore F27
    lfd  28, 112(1)  # restore F28
    lfd  29, 120(1)  # restore F29
    lfd  30, 128(1)  # restore F30
    lfd  31, 136(1)  # restore F31
    lfd  0,  144(1)  # load FPSCR
    mtfsf  0xff, 0  # restore FPSCR

    lwz  13, 152(1)  # restore R13
    lwz  14, 156(1)  # restore R14
    lwz  15, 160(1)  # restore R15
    lwz  16, 164(1)  # restore R16
    lwz  17, 168(1)  # restore R17
    lwz  18, 172(1)  # restore R18
    lwz  19, 176(1)  # restore R19
    lwz  20, 180(1)  # restore R20
    lwz  21, 184(1)  # restore R21
    lwz  22, 188(1)  # restore R22
    lwz  23, 192(1)  # restore R23
    lwz  24, 196(1)  # restore R24
    lwz  25, 200(1)  # restore R25
    lwz  26, 204(1)  # restore R26
    lwz  27, 208(1)  # restore R27
    lwz  28, 212(1)  # restore R28
    lwz  29, 216(1)  # restore R29
    lwz  30, 220(1)  # restore R30
    lwz  31, 224(1)  # restore R31
    lwz  3,  228(1)  # restore hidden

    # restore CR
    lwz   0, 232(1)
    mtcr  0
    # restore LR
    lwz   0, 236(1)
    mtlr  0
    # load PC
    lwz   0, 240(1)
    # restore CTR
    mtctr 0

    # adjust stack
    addi  1, 1, 244

    # return transfer_t 
    stw  6, 0(3)
    stw  5, 4(3)

    # jump to context
    bctr
